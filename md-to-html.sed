#!/bin/sed -Ef

# ![image](url)
s/! *\[(.*)\] *\((.+)\)/<img src="\2" alt="\1">/g
s/! *\((.+)\)/<img src="\1">/g

# ***text*** or ___text___
s/(^|[^\*_])[\*_]{3}([^\*_]+)[\*_]{3}([^\*_]|$)/\1<b><i>\2<\/i><\/b>\3/g

# **text** or __text__
s/(^|[^\*_])[\*_]{2}([^\*_]+)[\*_]{2}([^\*_]|$)/\1<b>\2<\/b>\3/g

# *text* or _text_
s/(^|[^\*_])[\*_]([^\*_]+)[\*_]([^\*_]|$)/\1<i>\2<\/i>\3/g

# `text`
s/(^|[^`])`([^`]+)`([^`]|$)/\1<code>\2<\/code>\3/g

# No headers means it's a normal paragraph
s/^[^# ]+.*/<p>&<\/p>/

# Headers
s/^ *#{6} *([^#].*)*$/<h6>\1<\/h6>/
s/^ *#{5} *([^#].*)*$/<h5>\1<\/h5>/
s/^ *#{4} *([^#].*)*$/<h4>\1<\/h4>/
s/^ *#{3} *([^#].*)*$/<h3>\1<\/h3>/
s/^ *#{2} *([^#].*)*$/<h2>\1<\/h2>/
s/^ *# *([^#].*)*$/<h1>\1<\/h1>/
